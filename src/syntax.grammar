@top Program { variant }

@skip { space | LineComment }

Variant {
  variant
}

variant_child {
  expr ";" |
  Stmt
}
variant_last_child {
  expr ";"? |
  Stmt
}

variant {
  variant_child* variant_last_child
}

Variants {
  "{" (Variant "/")* Variant "}"
}

expr {
  expr_inner |
  Annotation { expr_inner ":" type } |
  Variants
}

expr_inner {
  Integer |
  String |
  Variable |
  Nullary |
  Sexpr
}

Sexpr {
  "(" callee expr* ")" |
  "(" expr Opalias expr ")"
}

Stmt {
  callee expr+ ";" |
  expr Opalias expr ";"
}

callee {
  Builtin |
  Opalias |
  Variable
}

type {
  Range |
  Type_name |
  Type_sexpr
}

Opalias {
  opsymbol | @specialize<Builtin, "mod" | "rem" | "div" | "trunc_div">
}

Nullary {
  @specialize<Builtin, "argv" | "argc" | "true" | "false">
}

Range {
  ("-oo" | Integer) ".." ("oo" | Integer)
}

Type_sexpr {
  "(" Type_name (type | Integer)+ ")"
}

@tokens {
  Integer {
    "0" |
    (
      "-"? $[1-9] $[0-9]* ($[eE] $[1-9] $[0-9]*)?
    ) |
    (
      "-"? "0x" $[1-9a-fA-F] $[0-9a-fA-F]*
    ) |
    (
      "-"? "0b1" $[01]*
    )
  }

  String { '"' (!["\\] | "\\" _)* '"' }

  Variable {"$" $[a-zA-Z_]*}

  Type_name {$[A-Z] $[a-z]*}

  opsymbol {"<-" | "+" | "-" | "*" | "^" | "&" | "|" | "~" | ">>" | "<<" | "==" | "!=" | "<=" | "<" | ">=" | ">" | "=>" | "#" | ".."}

  Builtin {$[a-z_] $[a-z0-9_]*}

  LineComment { "%" ![\n]* }

  space { @whitespace+ }

  "oo"

  "-oo"

  @precedence { Integer, opsymbol }
}

@detectDelim
